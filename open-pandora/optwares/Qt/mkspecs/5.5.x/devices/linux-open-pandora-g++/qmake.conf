#
# qmake configuration for Open Pandora

include(../common/linux_device_pre.conf)

DEFINES += PANDORA

QMAKE_INCDIR += $$[QT_SYSROOT]/usr/include/freetype2
#QMAKE_LIBDIR +=

#QMAKE_LFLAGS += -Wl,-rpath-link,$$[QT_SYSROOT]/opt/vc/lib
QMAKE_LFLAGS += \
    -L$$[QT_SYSROOT]/lib \
    -L$$[QT_SYSROOT]/usr/lib \
    -lc_nonshared \
    -ldl

QMAKE_INCDIR_EGL = $$[QT_SYSROOT]/usr/include
QMAKE_LIBDIR_EGL = $$[QT_SYSROOT]/usr/lib

QMAKE_INCDIR_OPENGL_ES2 = $${QMAKE_INCDIR_EGL}
QMAKE_LIBDIR_OPENGL_ES2 = $${QMAKE_LIBDIR_EGL}

QMAKE_LIBS_EGL = -lEGL -lGLESv2

contains(DISTRO, squeeze) {
    #Debian Squeeze: Legacy everything
    QMAKE_LIBS_OPENGL_ES2 = -lGLESv2 -lEGL
} else:contains(DISTRO, arch) {
    #On principle: no wizardry required
} else {
    #This is not strictly necessary
    #DISTRO_OPTS += deb-multi-arch
    DISTRO_OPTS += soft-float
}

QMAKE_CFLAGS += \
    -marm \
    -mabi=aapcs-linux \
    -mfpu=neon \
    -mtune=cortex-a8 \
    -march=armv7-a \
    -mfloat-abi=softfp \
    -ftree-vectorize \
    -fsingle-precision-constant \
    -fsigned-char \
    -O3 \
    -pipe
QMAKE_CXXFLAGS = $$QMAKE_CFLAGS

# Preferred eglfs backend
# One of: eglfs_brcm  eglfs_kms  eglfs_mali  eglfs_viv  eglfs_x11
EGLFS_DEVICE_INTEGRATION = eglfs_x11

include(../common/linux_arm_device_post.conf)

load(qt_config)
